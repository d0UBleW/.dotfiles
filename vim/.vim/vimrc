set nocp

source $VIMRUNTIME/defaults.vim

runtime! ftplugin/man.vim

" Plugin

call plug#begin('~/.vim/plugged') " Installed plugins directory

Plug 'vim-utils/vim-man'

Plug 'jiangmiao/auto-pairs'

Plug 'wellle/targets.vim'

Plug 'tpope/vim-fugitive'

Plug 'tpope/vim-repeat'

Plug 'tpope/vim-surround'

Plug 'tpope/vim-commentary'

Plug 'tpope/vim-vinegar'

Plug 'tpope/vim-unimpaired'

Plug 'tpope/vim-sleuth'

Plug 'tpope/vim-dispatch'

Plug 'bkad/CamelCaseMotion'

Plug 'kana/vim-textobj-user'

Plug 'kana/vim-textobj-entire'

Plug 'kana/vim-textobj-line'

Plug 'uiiaoo/java-syntax.vim'

Plug 'petRUShka/vim-sage'

Plug 'vim-python/python-syntax'

" Plug 'ludovicchabant/vim-gutentags'

Plug 'pprovost/vim-ps1'

Plug 'mbbill/undotree'

Plug 'jelera/vim-javascript-syntax'

Plug 'andymass/vim-matchup'

Plug 'morhetz/gruvbox'

Plug 'sonph/onehalf', { 'rtp': 'vim/' }

Plug 'lifepillar/vim-solarized8'

Plug 'vim-pandoc/vim-pandoc'

Plug 'vim-pandoc/vim-pandoc-syntax'

Plug 'ap/vim-css-color'

Plug 'bfrg/vim-cpp-modern'

Plug 'vim-scripts/DoxygenToolkit.vim'

Plug 'airblade/vim-gitgutter'

Plug 'rhysd/vim-clang-format'

Plug 'vimpostor/vim-lumen'

Plug 'Yggdroot/indentLine'

Plug 'ryanoasis/vim-devicons'

Plug 'editorconfig/editorconfig-vim'

Plug 'neoclide/coc.nvim', {'branch': 'release'}

Plug 'airblade/vim-rooter'

call plug#end()

filetype plugin indent on

syntax on

" Cursor
let &t_SI .= "\<Esc>[6 q"
let &t_EI .= "\<Esc>[2 q"
let &t_SR .= "\<Esc>[4 q"

let mapleader = '\'
let maplocalleader = '|'

let &background = strftime("%H") < 18 ? "light" : "dark"


" Automatically save before commands like :next and :make
set autowrite

" Enable mouse in all mode
set mouse=a

" set t_Co=256    " Force 256 colours

set termguicolors

set nobackup

set noswapfile

" Disable line wrapping
set nowrap

" Avoid breaking word when line wrapping
set linebreak

" Show line break character
set showbreak=…

" Customize SpecialKey (tab, nbsp, trail, space) and NonText (eol, precedes, extends) characters
set listchars=tab:→\ ,eol:↲,nbsp:␣,trail:•,precedes:«,extends:»,space:·

" Smooth backspace experience
set backspace=indent,eol,start

" Enable line numbering
set nu rnu

" Indenting
set autoindent
set smartindent

" Tabbing
set smarttab
set tabstop=4
set shiftwidth=4
set softtabstop=4
set expandtab

set belloff=all

set wildmenu
set wildmode=longest:full,full
set wildoptions=pum,tagfile

" Buffer
set hidden

" Search
set incsearch
set ignorecase
set smartcase
set nohlsearch

" Split behaviour
set splitbelow
set splitright

set laststatus=2

set showcmd

set cursorline

set colorcolumn=80

set signcolumn=yes
" set signcolumn=number

set fillchars=vert:│,fold:·,eob:~"

" Keep cursor position when doing gg, G, CTRL_D, CTRL_U, >>, << , etc.
set nostartofline

set sidescroll=1
set scrolloff=19

set switchbuf=uselast

set autoread
set confirm

" onehalflight requires `set t_Co=256`
" colorscheme onehalflight

colorscheme solarized8

" My Function

function! InsertSemicolon()
    let line=getline('.')
    let semi=matchstr(line, ";$")
    if semi == ""
        execute "normal! mqA;\<esc>`q"
        delmarks q
    endif
endfunction


" Mapping

xnoremap * y/\V<C-R>"<del><CR>
xnoremap # y?\V<C-R>"<del><CR>

nnoremap <Leader>sr :%s/<C-r><C-w>//g<left><left>
nnoremap <silent> <leader>u :UndotreeToggle<CR>
nnoremap <silent> <leader>co :copen<CR>
nnoremap <silent> <leader>cc :cclose<CR>

nnoremap <space>h <C-w>h
nnoremap <space>j <C-w>j
nnoremap <space>k <C-w>k
nnoremap <space>l <C-w>l

nnoremap <silent> <Tab> :tabnext<CR>
nnoremap <silent> <S-Tab> :tabprevious<CR>

nnoremap Y y$
nnoremap n nzzzv
nnoremap N Nzzzv
nnoremap J mzJ`z

vnoremap <silent> <C-j> :m '>+1<CR>gv=gv
vnoremap <silent> <C-k> :m '<-2<CR>gv=gv
nnoremap <silent> <C-j> :m +1<CR>==
nnoremap <silent> <C-k> :m -2<CR>==
" inoremap <silent> <C-j> <esc>mz:m +1<CR>==`za
" inoremap <silent> <C-k> <esc>mz:m -2<CR>==`za

" Trigger iabbrev
inoremap <C-j> <C-]>

nnoremap <silent> \; :call InsertSemicolon()<CR>
inoremap <silent> ;<tab> <C-o>:call InsertSemicolon()<CR>

" Search Replace
nnoremap <Leader>sr :%s/<C-r><C-w>//g<Left><Left>

" Toggle Line numbering
nnoremap <silent> <leader>n :exec &nu==&rnu? "set nu!" : "set rnu!"<CR>

cnoremap <C-a> <Home>
cnoremap <c-e> <End>

" https://vim.fandom.com/wiki/Make_Vim_completion_popup_menu_work_just_like_in_an_IDE
" inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

" Fugitive
nmap <leader>gh :diffget //2<CR>
nmap <leader>gl :diffget //3<CR>
nmap <leader>gs :G<CR>

" Skeleton Templates

function! Java_Skel()
    0r $HOME/.vim/templates/skeleton.java
    s/FILENAME/\=expand("%:t:r")
    " execute "normal! 1j"
endfunction

function! Header_Skel()
    0r $HOME/.vim/templates/skeleton.h
    let l:filename = toupper(expand("%:t:r"))
    %s/FILENAME/\=expand(l:filename)
    execute "normal! Gddkk"
endfunction

if has("autocmd")
    augroup templates
        au!
        " au BufNewFile *.sh 0r ~/.vim/templates/skeleton.sh
        au BufNewFile *.html 0r ~/.vim/templates/skeleton.html
        au BufNewFile *.java call Java_Skel()
        au BufNewFile *.h call Header_Skel()
    augroup end
endif



" Misc

function! QuickfixMapping()
    " Go to the previous location and stay in the quickfix window
    nnoremap <buffer> K :cprev<CR>zz<C-w>w

    " Go to the next location and stay in the quickfix window
    nnoremap <buffer> J :cnext<CR>zz<C-w>w

    nnoremap <silent> <buffer> <localleader>q :bwipeout<CR>
endfunction

augroup quickfix_group
  autocmd!

  " Use custom keybindings
  autocmd filetype qf call QuickfixMapping()
augroup END

augroup helpft
    au!
    autocmd filetype help nnoremap <silent> <buffer> <localleader>q :bwipeout<CR>
    autocmd filetype help nnoremap <silent> <buffer> <F1> :bwipeout<CR>
augroup END

autocmd! BufWritePost vimrc source ~/.vim/vimrc

if executable('rg')
    set grepprg=rg\ --vimgrep\ --no-ignore
    set grepformat^=%f:%l:%c:%m
endif

" WSL yank support
let s:clip = '/mnt/c/Windows/System32/clip.exe'  " change this path according to your mount point
if executable(s:clip)
    augroup WSLYank
        autocmd!
        autocmd TextYankPost * if v:event.operator ==# 'y' | call system(s:clip, @0) | endif
    augroup END
endif

call mystatusline#SetStatusLine()

" Undo directory
function! Tmpwatch(path, days)
    let l:path = expand(a:path)
    if isdirectory(l:path)
        for file in globpath(l:path, '*', 1, 1)
            if localtime() > getftime(file) + 86400 * a:days && delete(file) != 0
                echo "Tmpwatch(): Error deleting '" . file . "'"
            endif
        endfor
    else
        echo "Tmpwatch(): Directory '" . l:path . "'not found"
    endif
endfunction

if exists("+undofile")
    if !isdirectory($HOME . '/.vim' . '/.undo')
        call system('mkdir ' . $HOME . '/.vim' . '/.undo')
    endif
    set undodir=~/.vim/.undo// ",/tmp//
    set undofile
    set undolevels=1000
    set undoreload=10000

    call Tmpwatch(&undodir, 31)
endif




" Plugin Config

" editorconfig
let g:EditorConfig_exclude_patterns = ['fugitive://.*', 'scp://.*']



" Camel
let g:camelcasemotion_key = '\'



" auto-pairs
let g:AutoPairsShortcutFastWrap = "<F12>"



" disable matchit
let g:loaded_matchit = 1



" matchup + surround
let g:matchup_surround_enabled = 1



" vim-python/python-syntax
let g:python_highlight_all = 1



" matchup
let g:matchup_matchpref = { 
            \ "html": { 
                \ "tagnameonly": 1 
                \ } }



" CoC
function! CheckBackspace() abort
  let col = col('.') - 1
  return !col || getline('.')[col - 1]  =~# '\s'
endfunction


inoremap <silent><expr> <TAB>
    \ pumvisible() ? "\<C-n>" :
    \ CheckBackspace() ? "\<TAB>" :
    \ coc#refresh()

inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

" Use <c-space> to trigger completion.
if has('nvim')
  inoremap <silent><expr> <c-space> coc#refresh()
else
  inoremap <silent><expr> <c-@> coc#refresh()
endif

" Make <CR> auto-select the first completion item and notify coc.nvim to
" format on enter, <cr> could be remapped by other vim plugin
inoremap <silent><expr> <cr> pumvisible() ? coc#_select_confirm()
                              \: "\<C-g>u\<CR>\<c-r>=coc#on_enter()\<CR>"

" Use `[g` and `]g` to navigate diagnostics
" Use `:CocDiagnostics` to get all diagnostics of current buffer in location list.
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" GoTo code navigation.
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window.
nnoremap <silent> K :call ShowDocumentation()<CR>

function! ShowDocumentation()
  if CocAction('hasProvider', 'hover')
    call CocActionAsync('doHover')
  else
    call feedkeys('K', 'in')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')

" Symbol renaming.
nmap <leader>rn <Plug>(coc-rename)

" Formatting selected code.
xmap <leader>f  <Plug>(coc-format-selected)
nmap <leader>f  <Plug>(coc-format-selected)

augroup mygroup
  autocmd!
  " Setup formatexpr specified filetype(s).
  autocmd FileType typescript,json setl formatexpr=CocAction('formatSelected')
  " Update signature help on jump placeholder.
  autocmd User CocJumpPlaceholder call CocActionAsync('showSignatureHelp')
augroup end

" Applying codeAction to the selected region.
" Example: `<leader>aap` for current paragraph
xmap <leader>a  <Plug>(coc-codeaction-selected)
nmap <leader>a  <Plug>(coc-codeaction-selected)

" Remap keys for applying codeAction to the current buffer.
nmap <leader>ac  <Plug>(coc-codeaction)
" Apply AutoFix to problem on the current line.
nmap <leader>qf  <Plug>(coc-fix-current)

" Run the Code Lens action on the current line.
nmap <leader>cl  <Plug>(coc-codelens-action)

" Map function and class text objects
" NOTE: Requires 'textDocument.documentSymbol' support from the language server.
xmap if <Plug>(coc-funcobj-i)
omap if <Plug>(coc-funcobj-i)
xmap af <Plug>(coc-funcobj-a)
omap af <Plug>(coc-funcobj-a)
xmap ic <Plug>(coc-classobj-i)
omap ic <Plug>(coc-classobj-i)
xmap ac <Plug>(coc-classobj-a)
omap ac <Plug>(coc-classobj-a)

" Remap <C-f> and <C-b> for scroll float windows/popups.
if has('nvim-0.4.0') || has('patch-8.2.0750')
  nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
  inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
  inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
  vnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  vnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
endif

" Use CTRL-S for selections ranges.
" Requires 'textDocument/selectionRange' support of language server.
nmap <silent> <C-s> <Plug>(coc-range-select)
xmap <silent> <C-s> <Plug>(coc-range-select)

" Add `:Format` command to format current buffer.
command! -nargs=0 Format :call CocActionAsync('format')

" Add `:Fold` command to fold current buffer.
command! -nargs=? Fold :call     CocAction('fold', <f-args>)

" Add `:OR` command for organize imports of the current buffer.
command! -nargs=0 OR   :call     CocActionAsync('runCommand', 'editor.action.organizeImport')

" Add (Neo)Vim's native statusline support.
" NOTE: Please see `:h coc-status` for integrations with external plugins that
" provide custom statusline: lightline.vim, vim-airline.
" set statusline^=%{coc#status()}%{get(b:,'coc_current_function','')}

" Mappings for CoCList
" Show all diagnostics.
nnoremap <silent><nowait> <space>a  :<C-u>CocList diagnostics<cr>
" Manage extensions.
nnoremap <silent><nowait> <space>e  :<C-u>CocList extensions<cr>
" Show commands.
nnoremap <silent><nowait> <space>c  :<C-u>CocList commands<cr>
" Find symbol of current document.
nnoremap <silent><nowait> <space>o  :<C-u>CocList outline<cr>
" Search workspace symbols.
nnoremap <silent><nowait> <space>s  :<C-u>CocList -I symbols<cr>
" Do default action for next item.
nnoremap <silent><nowait> zj  :<C-u>CocNext<CR>
" Do default action for previous item.
nnoremap <silent><nowait> zk  :<C-u>CocPrev<CR>
" Resume latest coc list.
nnoremap <silent><nowait> <space>p  :<C-u>CocListResume<CR>

function! SetupCommandAbbrs(from, to)
  exec 'cnoreabbrev <expr> '.a:from
        \ .' ((getcmdtype() ==# ":" && getcmdline() ==# "'.a:from.'")'
        \ .'? ("'.a:to.'") : ("'.a:from.'"))'
endfunction

" Use C to open coc config
call SetupCommandAbbrs('C', 'CocConfig')

nnoremap <silent> <space>y  :<C-u>CocList -A --normal yank<cr>



" indentLine
nnoremap yo<tab> :IndentLinesToggle<CR>

let g:indentLine_char_list = ['|', '¦', '┆', '┊']



" tpope/vim-commentary
augroup CommentaryFt
    autocmd!
    autocmd filetype json,cpp,c setlocal commentstring=\/\/\ %s
augroup END

